noindex: true
desc:Hackey Trackey Sample Playback Module (pre-alpha)
tags: tracker-style sampler
version: 0.70
author: Joep Vanlier
changelog: Include very basic autosplit functionality to Hackey Trackey Sample Playback.
license: MIT
about:
  # Hackey Tracker Sampler
  A small sampler designed to interoperate with Hackey Trackey.
  
  ### Usage instructions:
  This small JSFX acts as a sampler that interoperates with Hackey Trackey to 
  support tracker-like sample playback.
  
  Volume is sent on MIDI CC 9, while efects are sent on CC 12 (to indicate the 
  effect number) and CC 13 (to indicate the value). Note that due to the limitations 
  of the format, some concessions had to be made. Please read the effect list below 
  carefully and consider how this impacts your workflow.
  
  - Effect List
  01 - Portamento up
  02 - Portamento down
    Note that the portamento's behave different from Protracker. In PT you directly
    perform the portamento based on the period of the signal. Portamento is updated
    every N times per row (ticks).
    Here, the portamento slides continuously and the amount is specified in 
    eigth semitones. This means that 08 is 1 semitone. 10 is 2, etc.
    00 continues the last portamento.
  03 - Glide
    Glide to note. Glide speed is specified in 1/16th notes.
    00 Continues a previous glide.
  04 - Vibrato
    X is pitch depth (value from 0 to 7). They are given in seventh semitones.
    Y is speed (value from 0 to F).
      Continue, 128, 64, 32, 24, 16, 12, 8, 6, 5, 4, 3, 2, 1, 0.25, 0.125
    0 continues the previous value.
  08 - Panning
    Panning.
  09 - Set offset
    Unlike the classic Protracker, this sets offset as fraction of the sample length.
    Since 7F (127) is the maximum in MIDI; 40 is the middle of the sample, 20 1/4th etc.
  0A - Arpeggiator
    Arpeggiate. X and Y are note offsets in semitones. 0 continues previous value.
  0B - Retrigger
    Retrigger note.
    X - Volume reduction per trigger
    Y - Retrigger count
  0C - Sample probability
  60 - Reverse
    Reverses playhead if effect value is 00. Note that if this effect is applied on a 
    note start, it plays the sample from the end. If the effect is started from anywhere 
    else, it will simply reverse the playhead. When the effect is used with 01, hackey 
    tracker playback will return to forward playing.
    

options:gmem=saike_HT_sample
options:want_all_kb
options:no_meter
in_pin:left input
in_pin:right input
out_pin:left output 1
out_pin:right output 1
out_pin:left output 2
out_pin:right output 2
out_pin:left output 3
out_pin:right output 3
out_pin:left output 4
out_pin:right output 4
out_pin:left output 5
out_pin:right output 5
out_pin:left output 6
out_pin:right output 6
out_pin:left output 7
out_pin:right output 7
out_pin:left output 8
out_pin:right output 8
out_pin:left output 9
out_pin:right output 9
out_pin:left output 10
out_pin:right output 10
out_pin:left output 11
out_pin:right output 11
out_pin:left output 12
out_pin:right output 12
out_pin:left output 13
out_pin:right output 13
out_pin:left output 14
out_pin:right output 14
out_pin:left output 15
out_pin:right output 15
out_pin:left output 16
out_pin:right output 16

import htp_midi.jsfx-inc
import htp_sample_editor.jsfx-inc
options:maxmem=34000000

slider1:tracker=0<0,1,1{Buzz,Renoise}>-Key mapping
slider2:kb_layout=0<0,2,1{QWERTY,QWERTZ,AZERTY}>-Keyboard layout
slider3:multi_out=0<0,1,{Off,On}>-Multi-Out
slider4:select_by_notes=0<0,1,{Off,On}>-Choose sample by note


@init
init_sample_data_defaults();

font_color_r = font_color_g = font_color_b = 0.8;
font_color_a = 1.0;

waveform_r = .3;
waveform_g = .4;
waveform_b = .5;
waveform_a = 1.0;
box_rect_color_r = 0.02;
box_rect_color_g = 0.02;
box_rect_color_b = 0.02;
box_edge_color_r = 0.15;
box_edge_color_g = 0.15;
box_edge_color_b = 0.15;
selection_color_r = 0.1;
selection_color_g = 0.1;
selection_color_b = 0.5;
selection_color_a = 0.2;

(ticks_per_beat == 0) ? ticks_per_beat = 4;

function char_to_pitch(ch)
global(tracker)
(
  (tracker == 0) ? (
    (ch == 122) ? ( 24 ) : (ch == 90) ? ( 24 ) : (ch == 120) ? ( 26 ) : (ch == 88) ? ( 26 ) : (ch == 99) ? ( 28 ) : (ch == 67) ? ( 28 ) : (ch == 118) ? ( 29 ) : (ch == 86) ? ( 29 ) : (ch == 98) ? ( 31 ) : (ch == 66) ? ( 31 ) : (ch == 110) ? ( 33 ) : (ch == 78) ? ( 33 ) : (ch == 109) ? ( 35 ) : (ch == 77) ? ( 35 ) : (ch == 115) ? ( 25 ) : (ch == 83) ? ( 25 ) : (ch == 100) ? ( 27 ) : (ch == 68) ? ( 27 ) : (ch == 103) ? ( 30 ) : (ch == 71) ? ( 30 ) : (ch == 104) ? ( 32 ) : (ch == 72) ? ( 32 ) : (ch == 106) ? ( 34 ) : (ch == 74) ? ( 34 ) : (ch == 113) ? ( 36 ) : (ch == 81) ? ( 36 ) : (ch == 119) ? ( 38 ) : (ch == 87) ? ( 38 ) : (ch == 101) ? ( 40 ) : (ch == 69) ? ( 40 ) : (ch == 114) ? ( 41 ) : (ch == 82) ? ( 41 ) : (ch == 116) ? ( 43 ) : (ch == 84) ? ( 43 ) : (ch == 121) ? ( 45 ) : (ch == 89) ? ( 45 ) : (ch == 117) ? ( 47 ) : (ch == 85) ? ( 47 ) : (ch == 105) ? ( 48 ) : (ch == 73) ? ( 48 ) : (ch == 111) ? ( 50 ) : (ch == 79) ? ( 50 ) : (ch == 112) ? ( 52 ) : (ch == 80) ? ( 52 ) : -1
  ) : ( tracker == 1 ) ? (
    (ch == 122) ? ( 24 ) : (ch == 90) ? ( 24 ) : (ch == 120) ? ( 26 ) : (ch == 88) ? ( 26 ) : (ch == 99) ? ( 28 ) : (ch == 67) ? ( 28 ) : (ch == 118) ? ( 29 ) : (ch == 86) ? ( 29 ) : (ch == 98) ? ( 31 ) : (ch == 66) ? ( 31 ) : (ch == 110) ? ( 33 ) : (ch == 78) ? ( 33 ) : (ch == 109) ? ( 35 ) : (ch == 77) ? ( 35 ) : (ch == 44) ? ( 36 ) : (ch == 44) ? ( 36 ) : (ch == 46) ? ( 38 ) : (ch == 46) ? ( 38 ) : (ch == 47) ? ( 40 ) : (ch == 47) ? ( 40 ) : (ch == 115) ? ( 25 ) : (ch == 83) ? ( 25 ) : (ch == 100) ? ( 27 ) : (ch == 68) ? ( 27 ) : (ch == 103) ? ( 30 ) : (ch == 71) ? ( 30 ) : (ch == 104) ? ( 32 ) : (ch == 72) ? ( 32 ) : (ch == 106) ? ( 34 ) : (ch == 74) ? ( 34 ) : (ch == 108) ? ( 37 ) : (ch == 76) ? ( 37 ) : (ch == 59) ? ( 39 ) : (ch == 59) ? ( 39 ) : (ch == 113) ? ( 36 ) : (ch == 81) ? ( 36 ) : (ch == 119) ? ( 38 ) : (ch == 87) ? ( 38 ) : (ch == 101) ? ( 40 ) : (ch == 69) ? ( 40 ) : (ch == 114) ? ( 41 ) : (ch == 82) ? ( 41 ) : (ch == 116) ? ( 43 ) : (ch == 84) ? ( 43 ) : (ch == 121) ? ( 45 ) : (ch == 89) ? ( 45 ) : (ch == 117) ? ( 47 ) : (ch == 85) ? ( 47 ) : (ch == 105) ? ( 48 ) : (ch == 73) ? ( 48 ) : (ch == 111) ? ( 50 ) : (ch == 79) ? ( 50 ) : (ch == 112) ? ( 52 ) : (ch == 80) ? ( 52 ) : (ch == 91) ? ( 53 ) : (ch == 91) ? ( 53 ) : (ch == 93) ? ( 55 ) : (ch == 93) ? ( 55 ) : (ch == 50) ? ( 37 ) : (ch == 50) ? ( 37 ) : (ch == 51) ? ( 39 ) : (ch == 51) ? ( 39 ) : (ch == 53) ? ( 42 ) : (ch == 53) ? ( 42 ) : (ch == 54) ? ( 44 ) : (ch == 54) ? ( 44 ) : (ch == 55) ? ( 46 ) : (ch == 55) ? ( 46 ) : (ch == 57) ? ( 49 ) : (ch == 57) ? ( 49 ) : (ch == 48) ? ( 51 ) : (ch == 48) ? ( 51 ) : (ch == 61) ? ( 54 ) : (ch == 61) ? ( 54 ) : -1
  );
);

function get_pitch_from_kb(kb)
global(kb_layout)
(
  kb_layout == 0 ? (
    char_to_pitch(kb)
  ) : ( kb_layout == 1 ) ? (
    char_to_pitch((kb == 122) ? ( 121 ) : (kb == 121) ? ( 122 ) :  (kb == 90) ? ( 89 ) : (kb == 89) ? ( 90 ) : kb)
  ) : (
    char_to_pitch((kb == 97) ? ( 113 ) : (kb == 113) ? ( 97 ) :  (kb == 65) ? ( 81 ) : (kb == 81) ? ( 65 ) :  (kb == 122) ? ( 119 ) : (kb == 119) ? ( 122 ) :  (kb == 90) ? ( 87 ) : (kb == 87) ? ( 90 ) :  (kb == 109) ? ( 59 ) : (kb == 59) ? ( 109 ) :  (kb == 77) ? ( 59 ) : (kb == 59) ? ( 77 ) : kb)
  );
);

function set_pan(pan)
local(p)
global()
instance(lpan, rpan)
(
  pan > 0.99 ? pan = 1;
  p = 0.5 * $pi * pan;
  lpan = cos(p);
  rpan = sin(p);
);

function stop_playback()
instance(x_fade_position)
global(crossfade_samples)
(
  x_fade_position = - crossfade_samples;
);

function stop_sample()
instance(current_playback)
global()
(
  (current_playback == 1) ? (
    this.play1.stop_playback();
  ) : (
    this.play0.stop_playback();
  );
);

function chan_default(idx)
instance(vol, chan)
global()
local()
(
  vol = 1;
  chan = idx;
  this.set_pan(0.5);
);

!initialized ? (
 chan1.chan_default(1);
 chan2.chan_default(2);
 chan3.chan_default(3);
 chan4.chan_default(4);
 chan5.chan_default(5);
 chan6.chan_default(6);
 chan7.chan_default(7);
 chan8.chan_default(8);
 chan9.chan_default(9);
 chan10.chan_default(10);
 chan11.chan_default(11);
 chan12.chan_default(12);
 chan13.chan_default(13);
 chan14.chan_default(14);
 chan15.chan_default(15);
 chan16.chan_default(16);
 preview_channel.chan_default(17);
 initialized = 1;
 last_selected_sample = -1337;
);

preview_channel.stop_sample();
chan1.stop_sample(1);
chan2.stop_sample(2);
chan3.stop_sample(3);
chan4.stop_sample(4);
chan5.stop_sample(5);
chan6.stop_sample(6);
chan7.stop_sample(7);
chan8.stop_sample(8);
chan9.stop_sample(9);
chan10.stop_sample(10);
chan11.stop_sample(11);
chan12.stop_sample(12);
chan13.stop_sample(13);
chan14.stop_sample(14);
chan15.stop_sample(15);
chan16.stop_sample(16);

function start_playback(buffer_loc, playspeed, offset)
instance(start_ptr, x_fade_position, playing, fade_start, position, speed,
         loop_start, loop_stop, loop_type, stop_position)
global(crossfade_samples, SAMPLE_HEADER)
local(len)
(
  len = buffer_loc[];
  stop_position = len / 2 - 2;
  fade_start = buffer_loc + buffer_loc[] + SAMPLE_HEADER - 2 * playspeed * crossfade_samples;
  start_ptr = buffer_loc + SAMPLE_HEADER;
  
  loop_start = buffer_loc[3];
  loop_stop = buffer_loc[4];
  loop_type = buffer_loc[5];
  
  x_fade_position = crossfade_samples;
  position = offset;
  playing = 1;
  speed = playspeed;
);

function update_loop_parameters(buffer_loc)
instance(loop_start, loop_stop, loop_type)
(
  loop_start = buffer_loc[3];
  loop_stop = buffer_loc[4];
  loop_type = buffer_loc[5];
  
  // If we are not looping or looping forward, make sure we're not going back
  // in time (which could happen if we were coming from a bidi loop and would
  // result in a loud click if we move past the start of the sample (it has
  // the length there).
  ((loop_type == 0) || (loop_start == -1)) ? (
    speed = abs(speed);
  );
);

/* Sample play (each channel has two of these guys that alternate) */
function play()
instance(start_ptr, read_ptr, x_fade_position, fade_start,
         outL, outR, playing, fade_level, position, speed,
         loop_start, loop_stop, loop_type,
         portamento_len, portamento_ds, vib_len,
         vib_depth, vib_speed, vib_phase, stop_position)
global(crossfade_samples, pi_inv_crossfade_samples, eco, play_state)
local(frac, ism1, is0, is1, is2, id0, id1, id2, id3)
(
  // Positive fade means fading in
  (x_fade_position > 0.01) ? (
    fade_level = 1.0 - sqrt(0.5 - 0.5 * cos(x_fade_position * pi_inv_crossfade_samples));
    x_fade_position -= 1;
  // Negative fade means fading out
  ) : (x_fade_position < -0.01) ? (
    fade_level = sqrt(0.5 - 0.5 * cos(- x_fade_position * pi_inv_crossfade_samples));
    x_fade_position += 1;
    ((x_fade_position == 0) || (position > stop_position)) ? (
      playing = 0; // Terminate playback
      vib_len = 0;
      portamento_len = 0;
    );
  ) : (
    // Fade out if we're approaching the end of the recorded sample
    fade_level = 1;
    (loop_start > 0) ? (
      (loop_type == 0) ? (
        (position > loop_stop) ? position -= (loop_stop - loop_start);
      ) : (
        (position > loop_stop) ? (
          speed = - speed;
        ) : ( position < loop_start ) ? (
          speed = abs(speed);
        );
      );
    ) : (
      (read_ptr > fade_start) ? (
        x_fade_position = - crossfade_samples;
      );
    );
  );
  
  frac = position - floor(position);
  
  position > 1 ? (
    eco = 1;
    eco ? (
      read_ptr = start_ptr + 2 * floor(position) - 2;
      outL = (read_ptr[] * (1.0 - frac) + frac * read_ptr[2]);
      outR = (read_ptr[1] * (1.0 - frac) + frac * read_ptr[3]);
    ) : (
      read_ptr = start_ptr + 2 * floor(position) - 8;
      ism1 = read_ptr[];
      is0 = read_ptr[2];
      is1 = read_ptr[4];
      is2 = read_ptr[6];
      
      id0 = is0;
      id1 = 0.5*(is1 - ism1);
      id2 = ism1 - 2.5*is0 + 2*is1 - 0.5*is2;
      id3 = 0.5*(is2 - ism1) + 1.5 * (is0 - is1);
      outL = ((id3*frac+id2)*frac+id1)*frac+id0;
      
      ism1 = read_ptr[1];
      is0 = read_ptr[3];
      is1 = read_ptr[5];
      is2 = read_ptr[7];
      
      id0 = is0;
      id1 = 0.5*(is1 - ism1);
      id2 = ism1 - 2.5*is0 + 2*is1 - 0.5*is2;
      id3 = 0.5*(is2 - ism1) + 1.5 * (is0 - is1);
      outR = ((id3*frac+id2)*frac+id1)*frac+id0;
    );
  ) : (
    /* Gentle fade */
    outL *= 0.9995;
    outR *= 0.9995;
  );
  
  outL *= fade_level;
  outR *= fade_level;
  
  (vib_len > 0) ? (
    (vib_len == 1) ? ( vib_phase = -1337; )
    : (
      vib_phase += vib_speed;
      (vib_phase > 1) ? vib_phase -= 2;
      position += speed * exp(vib_depth * (2 * abs(vib_phase) - 1));
    );
    vib_len -= 1;
  ) : (
    position += speed;
  );
  
  portamento_len > 0 ? (
    portamento_len -= 1;
    speed *= portamento_ds;
  );
  
  fade_level
);

/* Channel play */
function prepare_channel_for_messages()
instance(offset, got_note, effect, effect_value)
global()
local()
(
  offset = 0;
  got_note = 0;
  effect_value = -1;
);

function schedule_note(new_sample_idx, new_pitch)
instance(got_note, sample_idx, pitch)
local()
global()
(
  pitch = new_pitch;
  sample_idx = new_sample_idx;
  got_note = 1;
);

function schedule_stop()
instance(got_note)
local()
global()
(
  (got_note == 0) ? ( got_note = -1; );
);

function pitch_to_speed(sample_location, pitch)
local(fs, fnote, r_speed, speed)
global(srate)
(
  fs = sample_location[1];
  fnote = sample_location[2];
  r_speed = 2^((pitch - fnote)/12);
  speed = r_speed * (fs / srate);
);

function vibrato_speed(speed_idx)
local()
global()
(
  (speed_idx == 1) ? ( 0.0625 ) : (speed_idx == 2) ? ( 0.08333333333333333 ) : (speed_idx == 3) ? ( 0.125 ) : (speed_idx == 4) ? ( 0.16666666666666666 ) : (speed_idx == 5) ? ( 0.25 ) : (speed_idx == 6) ? ( 0.3333333333333333 ) : (speed_idx == 7) ? ( 0.4 ) : (speed_idx == 8) ? ( 0.5 ) : (speed_idx == 9) ? ( 0.6666666666666666 ) : (speed_idx == 10) ? ( 1.0 ) : (speed_idx == 11) ? ( 1.3333333333333333 ) : (speed_idx == 12) ? ( 2.0 ) : (speed_idx == 13) ? ( 4.0 ) : (speed_idx == 14) ? ( 8.0 ) : (speed_idx == 15) ? ( 16.0 )
);

function commit_note()
instance(current_playback, sample_location, speed, offset,
         got_note, effect_value, effect,
         last_porta,
         last_arp_1, last_arp_2,
         last_vib_speed, last_vib_depth,
         vol, chan,
         pitch, sample_idx,
         note_schedule, note_schedule_ptr, next_backlog,
         playing)
global(samples_per_beat, samplelocs, blocked_samples, srate)
local(vol_change,
      portamento_ds, curspeed, portamento_len, count, cv, dt,
      vib_depth, vib_speed)
(
  note_schedule[] = -1337;

  (effect == 12) ? (
    (rand() > (effect_value / 127)) ? got_note = 0;
  );

  // Warning: this is a regression waiting to happen. Glide disables
  // note offs, but should not disable pitch computation from happening
  // since this is needed to know where to go.
  got_note ? (
    sample_location = samplelocs[sample_idx];
    speed = pitch_to_speed(sample_location, pitch);
  );

  /* If no effect value is set, don't use one */
  (effect_value == -1) ? effect = -1;

  // Start from end of sample if we are dealing with a reverse on a note start.
  (effect == 96 && effect_value == 0) ? offset = 1;

  (effect == 9) ? (offset = effect_value / 127;);
  
  // Glide disables the note on and off. See note on got_note above.
  (effect == 3) ? ( got_note = 0; );
  
  // Check whether this has a valid samplerate
  (got_note > 0) ? (
    sample_location[1] > 90 ? (
      (current_playback == 1) ? (
        (!blocked_samples[sample_idx] || (chan == 17)) ? (
          this.play0.start_playback(sample_location, speed, 0.5 * offset * sample_location[]);
        );
        this.play1.stop_playback();
      ) : (
        (!blocked_samples[sample_idx] || (chan == 17)) ? (
          this.play1.start_playback(sample_location, speed, 0.5 * offset * sample_location[]);
        );
        this.play0.stop_playback();
      );
      current_playback = 1.0 - current_playback;
    );
  ) : ( got_note < 0 ) ? (
    // No note, but a stop signal, then terminate the sample.
    this.stop_sample();
  );
  
  // Set portamento for upcoming row
  ((effect == 1) || (effect == 2)) ? (
    (effect_value == 0) ? effect_value = last_porta;
    // Semitone given by effect_value / 8. Hence 2^(value / (8*12))
    (effect == 1) ? (
      portamento_ds = 2^(effect_value / (8*12 * samples_per_beat));
    ) : (
      portamento_ds = 2^(-effect_value / (8*12 * samples_per_beat));
    );

    (current_playback == 0) ? (
      this.play0.portamento_len = samples_per_beat;
      this.play0.portamento_ds = portamento_ds;
    ) : (
      this.play1.portamento_len = samples_per_beat;
      this.play1.portamento_ds = portamento_ds;
    );
    last_porta = effect_value;
  );
  
  (effect == 4) ? (
    // The log 2 is to make sure we can use exp, instead of 2^in the sample function.
    count = effect_value / 16;
    vib_depth = count > 1 ? log(2) * floor(count) / (7 * 12) : last_vib_depth;
    count = effect_value % 16;
    vib_speed = count > 1 ? vibrato_speed(count) / samples_per_beat : last_vib_speed;
    
    (current_playback == 0) ? (
      (this.play0.vib_phase < -1336) ? this.play0.vib_phase = 0.5;
      this.play0.vib_depth = vib_depth;
      this.play0.vib_speed = vib_speed;
      this.play0.vib_len = samples_per_beat + 2;
    ) : (
      (this.play1.vib_phase < -1336) ? this.play1.vib_phase = 0.5;
      this.play1.vib_depth = vib_depth;
      this.play1.vib_speed = vib_speed;
      this.play1.vib_len = samples_per_beat + 2;
    );
    
    last_vib_speed = vib_speed;
    last_vib_depth = vib_depth;
  );
  
  (effect == 8) ? (
    this.set_pan(effect_value/128);
  );
  
  // Glide
  (effect == 3) ? (
    (effect_value == 0) ? effect_value = last_porta;
    curspeed = (current_playback == 0) ? this.play0.speed : this.play1.speed;
    portamento_ds = 2^(sign(speed - curspeed) * effect_value / (8 * 12 * samples_per_beat));
    portamento_len = min((log(speed) - log(curspeed)) / log(portamento_ds), samples_per_beat);
    (current_playback == 0) ? (
      this.play0.portamento_len = portamento_len;
      this.play0.portamento_ds = portamento_ds;
    ) : (
      this.play1.portamento_len = portamento_len;
      this.play1.portamento_ds = portamento_ds;
    );
    last_porta = effect_value;
  );
  
  (effect == 96) ? (
    (current_playback == 0) ? (
      (effect_value == 0) ? (
        this.play0.speed = - abs(this.play0.speed);
      ) : (
        this.play0.speed = abs(this.play0.speed);
      );
    ) : (
      (effect_value == 0) ? (
        this.play1.speed = - abs(this.play1.speed);
      ) : (
        this.play1.speed = abs(this.play1.speed);
      );
    );
  );
  
  // Arpeggiator
  (effect == 10) ? (
    count = floor(effect_value / 16);
    count > 0 ? last_arp_1 = count;
    
    count = effect_value % 16;
    count > 0 ? last_arp_2 = count;
    
    dt = floor(samples_per_beat / 3);
    (current_playback == 1) ? (
      this.play1.speed = pitch_to_speed(sample_location, pitch);
    ) : (
      this.play0.speed = pitch_to_speed(sample_location, pitch);
    );
    
    note_schedule_ptr = note_schedule;
    note_schedule_ptr[] = dt; note_schedule_ptr += 1;
    note_schedule_ptr[] = vol; note_schedule_ptr += 1; // Volume
    note_schedule_ptr[] = sample_idx; note_schedule_ptr += 1; // Sample
    note_schedule_ptr[] = - pitch - last_arp_1; note_schedule_ptr += 1; // Pitch
    
    note_schedule_ptr[] = dt; note_schedule_ptr += 1;
    note_schedule_ptr[] = vol; note_schedule_ptr += 1; // Volume
    note_schedule_ptr[] = sample_idx; note_schedule_ptr += 1; // Sample
    note_schedule_ptr[] = - pitch - last_arp_2; note_schedule_ptr += 1; // Pitch
    
    note_schedule_ptr[] = dt; note_schedule_ptr += 1;
    note_schedule_ptr[] = vol; note_schedule_ptr += 1; // Volume
    note_schedule_ptr[] = sample_idx; note_schedule_ptr += 1; // Sample
    note_schedule_ptr[] = - pitch; note_schedule_ptr += 1; // Pitch
    
    note_schedule_ptr[] = -1337;
    
    next_backlog = dt;
    note_schedule_ptr = note_schedule;
  );
  
  // Retrigger
  (effect == 11) ? (
    note_schedule_ptr = note_schedule;
    
    vol_change = (1.0 - floor(effect_value / 16) / 8);
    count = effect_value % 16;
    dt = floor(samples_per_beat / (count + 1));
    cv = vol;
    loop(count,
      note_schedule_ptr[] = dt; note_schedule_ptr += 1;
      note_schedule_ptr[] = cv; note_schedule_ptr += 1; // Volume
      cv *= vol_change;
      note_schedule_ptr[] = sample_idx; note_schedule_ptr += 1; // Sample
      note_schedule_ptr[] = pitch; note_schedule_ptr += 1; // Pitch
    );
    // Finalize with the volume after the trigger.
    note_schedule_ptr[] = dt - 1; note_schedule_ptr += 1;
    note_schedule_ptr[] = vol; note_schedule_ptr += 1;
    note_schedule_ptr[] = -1337;
    
    next_backlog = dt;
    note_schedule_ptr = note_schedule;
  );
  
  playing = this.play0.playing || this.play1.playing;
);

function set_offset_raw(offset)
local()
instance(current_playback)
(
  (current_playback == 1) ? (
    this.play1.position = offset
  ) : (
    this.play0.position = offset
  );
);

function play_sample(sample_idx, new_pitch, offset)
instance(current_playback)
global(samplelocs)
local()
(
  new_pitch > 0 ? (
    this.prepare_channel_for_messages();
    this.schedule_note(sample_idx, new_pitch);
    this.offset = offset;
    this.commit_note();
  ) : (
    (current_playback == 1) ? (
      this.play1.speed = pitch_to_speed(samplelocs[sample_idx], -new_pitch);
    ) : (
      this.play0.speed = pitch_to_speed(samplelocs[sample_idx], -new_pitch);
    );
  );
);

/* Channel update loop */
function channel_update_loop()
instance(current_playback, sample_location)
global()
local()
(
  (current_playback == 0) ? (
    this.play0.update_loop_parameters(sample_location);
  ) : (
    this.play1.update_loop_parameters(sample_location);
  );
);

function handle_message(msg1, msg2, msg3)
instance(vol, effect, effect_value)
local(cc_msg)
global(N_SAMPLES, select_by_notes)
(
  (msg1>$x8F && msg1<$xA0 && msg3!=0) ? (
    // Note on?
    select_by_notes ? (
      (msg2 - 60) < N_SAMPLES ? (
        this.schedule_note(msg2 - 60, 69);
      )
    ) : (
      (msg3 < N_SAMPLES) ? (
        this.schedule_note(msg3 - 1, msg2); /* msg3 = velocity which serves as note now ; msg2 is pitch */
      );
    );
  ) : ( (msg1>$x7F && msg1<$x90) || (msg1>$x89&&msg1<$xA0 && msg3==0 ) ) ? (
    // Note off
    this.schedule_stop();
  ) : (msg1>$xAF && msg1<$xC0) ? (
    // Set volume
    cc_msg = msg2 & 127;
    (cc_msg == 7) ? (
      vol = (msg3 & 127) / 127;
      vol *= vol;  // Square volume
    ) : (cc_msg == 12) ? (
      effect = msg3 & 127;
    ) : (cc_msg == 13) ? (
      effect_value = msg3 & 127;
    );
  );
);

function processBacklog()
instance(note_schedule, note_schedule_ptr, next_backlog)
local(chan, vol, sample_idx)
global()
(
  // Count down to the next item
  next_backlog -= 1;
    
  // FIRE!
  next_backlog == 0 ? (
    // What channel is this?
    note_schedule_ptr += 1; // Go past the count
    vol = note_schedule_ptr[]; note_schedule_ptr += 1;
      
    // Is there a note here or not?
    sample_idx = note_schedule_ptr[];
    (sample_idx > -1) ? (
      note_schedule_ptr += 1;
      this.play_sample(sample_idx, note_schedule_ptr[], 0);
      note_schedule_ptr += 1;
    );
      
    // Proceed to the next one
    next_backlog = note_schedule_ptr[];
  );
);

function play_channel()
instance(vol, cleft, cright, lpan, rpan, next_backlog)
global(ssl, ssr)
(
  next_backlog > 0 ? ( this.processBacklog() );

  cleft = 0.998 * cleft + 0.002 * lpan * vol;
  cright = 0.998 * cright + 0.002 * rpan * vol;
  
  this.play0.playing ? (
    this.play0.play();
    ssl += this.play0.outL * cleft;
    ssr += this.play0.outR * cright;
  );
  this.play1.playing ? (
    this.play1.play();
    ssl += this.play1.outL * cleft;
    ssr += this.play1.outR * cright;
  );
);

function initNoteSchedule(mem)
local()
global()
instance(note_schedule)
(
  note_schedule = mem;
  note_schedule[] = -1337;
  mem + 256;
);

freemem = 0;

// Sample locations are recorded as [64 byte header (first is length), sampledata]
freemem = (samplelocs = freemem) + 128;
idx = 0;
loop(N_SAMPLES,
  freemem = (samplelocs[idx] = freemem) + SAMPLE_SIZE;
  idx += 1;
);

freemem = (scratchloc = freemem) + SAMPLE_SIZE;
freemem = (copyloc = freemem) + SAMPLE_SIZE;
freemem = (undo_memory = freemem) + SAMPLE_SIZE + 64;

freemem = (midimem = freemem) + 32768;
midi.initializeMIDI(midimem, 1, 1);
//freemem = (note_schedule = freemem) + 256;
//note_schedule[] = -1337;
freemem = chan1.initNoteSchedule(freemem);
freemem = chan2.initNoteSchedule(freemem);
freemem = chan3.initNoteSchedule(freemem);
freemem = chan4.initNoteSchedule(freemem);
freemem = chan5.initNoteSchedule(freemem);
freemem = chan6.initNoteSchedule(freemem);
freemem = chan7.initNoteSchedule(freemem);
freemem = chan8.initNoteSchedule(freemem);
freemem = chan9.initNoteSchedule(freemem);
freemem = chan10.initNoteSchedule(freemem);
freemem = chan11.initNoteSchedule(freemem);
freemem = chan12.initNoteSchedule(freemem);
freemem = chan13.initNoteSchedule(freemem);
freemem = chan14.initNoteSchedule(freemem);
freemem = chan15.initNoteSchedule(freemem);
freemem = chan16.initNoteSchedule(freemem);
freemem = preview_channel.initNoteSchedule(freemem);
freemem = (blocked_samples = freemem) + N_SAMPLES;
freemem = (muted_samples = freemem) + N_SAMPLES;
freemem = (solod_samples = freemem) + N_SAMPLES;

freemem = (autosplits = freemem) + 128;

@slider

@serialize
writing = file_avail(0) < 0;
loaded = 1;
file_var(0, version);
serialize_all_samples(writing);
file_mem(0, muted_samples, N_SAMPLES);
file_mem(0, solod_samples, N_SAMPLES);
update_mute_status();

@block
midi.processMIDIBlock();

@sample
samples_per_beat = floor((srate * 60) / tempo / ticks_per_beat);

function processMIDISample()
local(channel)
instance(notePtr, remainingNotes, nextNote, curSample)
(
  // Take notes from the stack until we hit the end marker -1
  (remainingNotes) ? (
    // This handleMessages is ugly, but if we don't do this, we're going
    // to be checking this every cycle where there is any MIDI in the block => Not good.
    handleMessages = 0;
    (nextNote == curSample) ? (
      handleMessages = 1;
      chan1.prepare_channel_for_messages();
      chan2.prepare_channel_for_messages();
      chan3.prepare_channel_for_messages();
      chan4.prepare_channel_for_messages();
      chan5.prepare_channel_for_messages();
      chan6.prepare_channel_for_messages();
      chan7.prepare_channel_for_messages();
      chan8.prepare_channel_for_messages();
      chan9.prepare_channel_for_messages();
      chan10.prepare_channel_for_messages();
      chan11.prepare_channel_for_messages();
      chan12.prepare_channel_for_messages();
      chan13.prepare_channel_for_messages();
      chan14.prepare_channel_for_messages();
      chan15.prepare_channel_for_messages();
      chan16.prepare_channel_for_messages();
    );
  
    while(nextNote == curSample) (
      notePtr += 1;
      
      channel = notePtr[];
      (channel == 1) ? ( chan1.handle_message(notePtr[1], notePtr[2], notePtr[3]); )
      : (channel == 2) ? ( chan2.handle_message(notePtr[1], notePtr[2], notePtr[3]); )
      : (channel == 3) ? ( chan3.handle_message(notePtr[1], notePtr[2], notePtr[3]); )
      : (channel == 4) ? ( chan4.handle_message(notePtr[1], notePtr[2], notePtr[3]); )
      : (channel == 5) ? ( chan5.handle_message(notePtr[1], notePtr[2], notePtr[3]); )
      : (channel == 6) ? ( chan6.handle_message(notePtr[1], notePtr[2], notePtr[3]); )
      : (channel == 7) ? ( chan7.handle_message(notePtr[1], notePtr[2], notePtr[3]); )
      : (channel == 8) ? ( chan8.handle_message(notePtr[1], notePtr[2], notePtr[3]); )
      : (channel == 9) ? ( chan9.handle_message(notePtr[1], notePtr[2], notePtr[3]); )
      : (channel == 10) ? ( chan10.handle_message(notePtr[1], notePtr[2], notePtr[3]); )
      : (channel == 11) ? ( chan11.handle_message(notePtr[1], notePtr[2], notePtr[3]); )
      : (channel == 12) ? ( chan12.handle_message(notePtr[1], notePtr[2], notePtr[3]); )
      : (channel == 13) ? ( chan13.handle_message(notePtr[1], notePtr[2], notePtr[3]); )
      : (channel == 14) ? ( chan14.handle_message(notePtr[1], notePtr[2], notePtr[3]); )
      : (channel == 15) ? ( chan15.handle_message(notePtr[1], notePtr[2], notePtr[3]); )
      : (channel == 16) ? ( chan16.handle_message(notePtr[1], notePtr[2], notePtr[3]); );
      notePtr += 4;
      
      // Avoid constantly dereferencing by picking up the next one
      nextNote = notePtr[];
      remainingNotes = nextNote != -1337;
    );
    
    handleMessages ? (
      chan1.commit_note();
      chan2.commit_note();
      chan3.commit_note();
      chan4.commit_note();
      chan5.commit_note();
      chan6.commit_note();
      chan7.commit_note();
      chan8.commit_note();
      chan9.commit_note();
      chan10.commit_note();
      chan11.commit_note();
      chan12.commit_note();
      chan13.commit_note();
      chan14.commit_note();
      chan15.commit_note();
      chan16.commit_note();
    );
  );
  
  curSample += 1;
);
ssl = ssr = 0;

midi.processMIDISample();

multi_out ? (
  /* TODO: clean this up */
  chan1.playing ? (ssl = ssr = 0; chan1.play_channel(); spl0 = ssl; spl1 = ssr;);
  chan2.playing ? (ssl = ssr = 0; chan2.play_channel(); spl2 = ssl; spl3 = ssr;);
  chan3.playing ? (ssl = ssr = 0; chan3.play_channel(); spl4 = ssl; spl5 = ssr;);
  chan4.playing ? (ssl = ssr = 0; chan4.play_channel(); spl6 = ssl; spl7 = ssr;);
  chan5.playing ? (ssl = ssr = 0; chan5.play_channel(); spl8 = ssl; spl9 = ssr;);
  chan6.playing ? (ssl = ssr = 0; chan6.play_channel(); spl10 = ssl; spl11 = ssr);
  chan7.playing ? (ssl = ssr = 0; chan7.play_channel(); spl12 = ssl; spl13 = ssr;);
  chan8.playing ? (ssl = ssr = 0; chan8.play_channel(); spl14 = ssl; spl15 = ssr;);
  chan9.playing ? (ssl = ssr = 0; chan9.play_channel(); spl16 = ssl; spl17 = ssr;);
  chan10.playing ? (ssl = ssr = 0; chan10.play_channel(); spl18 = ssl; spl19 = ssr;);
  chan11.playing ? (ssl = ssr = 0; chan11.play_channel(); spl20 = ssl; spl21 = ssr;);
  chan12.playing ? (ssl = ssr = 0; chan12.play_channel(); spl22 = ssl; spl23 = ssr;);
  chan13.playing ? (ssl = ssr = 0; chan13.play_channel(); spl24 = ssl; spl25 = ssr;);
  chan14.playing ? (ssl = ssr = 0; chan14.play_channel(); spl26 = ssl; spl27 = ssr;);
  chan15.playing ? (ssl = ssr = 0; chan15.play_channel(); spl28 = ssl; spl29 = ssr;);
  chan16.playing ? (ssl = ssr = 0; chan16.play_channel(); spl30 = ssl; spl31 = ssr;);
  
  ssl = ssr = 0; 
  preview_channel.play_channel();
  spl0 += ssl;
  spl1 += ssr;
) : (
  chan1.playing ? chan1.play_channel();
  chan2.playing ? chan2.play_channel();
  chan3.playing ? chan3.play_channel();
  chan4.playing ? chan4.play_channel();
  chan5.playing ? chan5.play_channel();
  chan6.playing ? chan6.play_channel();
  chan7.playing ? chan7.play_channel();
  chan8.playing ? chan8.play_channel();
  chan9.playing ? chan9.play_channel();
  chan10.playing ? chan10.play_channel();
  chan11.playing ? chan11.play_channel();
  chan12.playing ? chan12.play_channel();
  chan13.playing ? chan13.play_channel();
  chan14.playing ? chan14.play_channel();
  chan15.playing ? chan15.play_channel();
  chan16.playing ? chan16.play_channel();
  preview_channel.play_channel();
  spl0 += ssl;
  spl1 += ssr;
);

@gfx
over_pad = -1;
SAMPLE_FONT = 5;
TINY_FONT = 6;
gfx_setfont(TINY_FONT, "Arial", 12);
BIG_FONT = 7;
gfx_setfont(BIG_FONT, "Arial", 18);

handle_dropped_files();
current_char = gfx_getchar();
handle_gmem_imports();

function handle_play_stop(x, y, w, h, idx)
local(bh, bw)
global(TINY_FONT, last_cap, mouse_cap, captured,
       preview_channel.play_sample, preview_channel.stop_sample)
(
  bw = 30;
  bh = 15;
  control_rect(x, y + h - bh, bw, bh, 0.04, 0.07, 0.03, 1.0, TINY_FONT, "Play", 0) ? (
    ((last_cap & 1) == 0) && (mouse_cap & 1) && !captured ? (
      preview_channel.play_sample(idx, 69, 0);
    );
  );
    
  control_rect(x + bw, y + h - bh, bw, bh, 0.07, 0.04, 0.03, 1.0, TINY_FONT, "Stop", 0) ? (
    ((last_cap & 1) == 0) && (mouse_cap & 1) && !captured ? (
      preview_channel.stop_sample();
    );
  );
);

// Are we playing anything?
new_pitch = get_pitch_from_kb(current_char);
((new_pitch > -1) && (wait_for_release != current_char) && gfx_getchar(current_char)) ? (
  preview_channel.play_sample(selected_sample, new_pitch + 69 - 24, 0);
  preview_offset = min(close_up.block_start, close_up.block_end);
  preview_offset > 0 ? (
    preview_channel.set_offset_raw((preview_offset - close_up.sample_start) * 0.5);
  );
  wait_for_release = current_char;
);

(!gfx_getchar(wait_for_release) && (wait_for_release > 0)) ? (
  wait_for_release = 0;
  preview_channel.stop_sample();
);

gfx_pad = 0.01 * gfx_w;

gfx_set(0, 0, 0, 1);
gfx_rect(0, 0, gfx_w, gfx_h);

nx = 6;
ny = 6;
block_pad = 4;
sample_edit_size = 0.3 * gfx_h;
block_width = floor((gfx_w - 2 * gfx_pad - (nx - 2) * block_pad) / nx);
block_height = floor((gfx_h - 2 * gfx_pad - (ny + 1 - 2) * block_pad - sample_edit_size) / ny);

cx = gfx_pad;
cy = gfx_pad;
idx = 0;
loop(ny,
  cx = gfx_pad;
  loop(nx,
    draw_wavebox(cx, cy, block_width, block_height, idx, samplelocs[idx]);
    process_pad(cx, cy, block_width, block_height, idx, samplelocs[idx]);
    process_pad_mute(cx, cy, block_width, block_height, idx, samplelocs[idx]);
    handle_play_stop(cx, cy, block_width, block_height, idx);
    cx += block_width + block_pad;
    idx += 1;
  );
  cy += block_height + block_pad;
);

updated_loop ? (
  updated_loop = 0;
  chan1.channel_update_loop();
  chan2.channel_update_loop();
  chan3.channel_update_loop();
  chan4.channel_update_loop();
  chan5.channel_update_loop();
  chan6.channel_update_loop();
  chan7.channel_update_loop();
  chan8.channel_update_loop();
  chan9.channel_update_loop();
  chan10.channel_update_loop();
  chan11.channel_update_loop();
  chan12.channel_update_loop();
  chan13.channel_update_loop();
  chan14.channel_update_loop();
  chan15.channel_update_loop();
  chan16.channel_update_loop();
  
  preview_channel.channel_update_loop();
);

/* Zoomed in waveform */
close_up.draw_sample_big(gfx_pad, cy, gfx_w - 2 * gfx_pad, sample_edit_size, reset_zoom);

function draw_playmarker(sample_idx, rel_pos)
local(x_pos, y_pos)
global(nx, ny, gfx_pad, block_pad, block_width, block_height,
       selected_sample, cy, gfx_w, gfx_pad, sample_edit_size)
(
  y_pos = floor(sample_idx / nx);
  x_pos = sample_idx - y_pos * nx;
  
  x_pos = gfx_pad + (block_width + block_pad) * x_pos;
  y_pos = gfx_pad + (block_height + block_pad) * y_pos;
  gfx_set(1, 1, 1, .03);
  gfx_rect(x_pos, y_pos, block_width, block_height);
  gfx_set(.3, .5, 1, .33);
  gfx_rect(x_pos + rel_pos * block_width - 1, y_pos, 3, block_height);
  gfx_set(.5, .5, 1, 1);
  gfx_rect(x_pos + rel_pos * block_width, y_pos, 1, block_height);
  
  // Also draw in the big preview
  selected_sample == sample_idx ? (
    x_pos = gfx_pad;
    y_pos = cy;
    gfx_set(.3, .5, 1, .33);
    gfx_rect(x_pos + rel_pos * (gfx_w - 2 * gfx_pad) - 1, y_pos, 3, sample_edit_size);
    gfx_set(.5, .5, 1, 1);
    gfx_rect(x_pos + rel_pos * (gfx_w - 2 * gfx_pad), y_pos, 1, sample_edit_size);
  );
);

function draw_markers()
local()
global(samplelocs)
instance(sample_idx, current_playback, play0, play1)
(
  (current_playback == 0) ? (
    play0.playing && (play0.position > 0) ? (
      draw_playmarker(sample_idx, 2 * play0.position / samplelocs[sample_idx][]);
    );
  ) : (
    play1.playing && (play1.position > 0) ? (
      draw_playmarker(sample_idx, 2 * play1.position / samplelocs[sample_idx][]);      
    );
  );
);

chan1.draw_markers();
chan2.draw_markers();
chan3.draw_markers();
chan4.draw_markers();
chan5.draw_markers();
chan6.draw_markers();
chan7.draw_markers();
chan8.draw_markers();
chan9.draw_markers();
chan10.draw_markers();
chan11.draw_markers();
chan12.draw_markers();
chan13.draw_markers();
chan14.draw_markers();
chan15.draw_markers();
chan16.draw_markers();
preview_channel.draw_markers();

sample_data_gfx_finalize();

handle_mouse_sample_handler();

(current_char == 26161) && (last_char != current_char) ? show_hints = 1 - show_hints;

show_hints ? (
gfx_setfont(10, "Arial", floor(gfx_h / 40));
str = 
"Effect List
  
  01 - Portamento up
  02 - Portamento down
    Note that the portamento's behave different from Protracker. In PT you directly
    perform the portamento based on the period of the signal. Portamento is updated
    every N times per row (ticks).
    Here, the portamento slides continuously and the amount is specified in 
    eigth semitones. This means that 08 is 1 semitone. 10 is 2, etc.
    00 continues the last portamento.
  03 - Glide
    Glide to note. Glide speed is specified in 1/16th notes.
    00 Continues a previous glide.
  04 - Vibrato
    X is pitch depth (value from 0 to 7). They are given in seventh semitones.
    Y is speed (value from 0 to F).
      Continue, 128, 64, 32, 24, 16, 12, 8, 6, 5, 4, 3, 2, 1, 0.25, 0.125
    0 continues the previous value.
  08 - Panning
    Panning.
  09 - Set offset
    Unlike the classic Protracker, this sets offset as fraction of the sample length.
    Since 7F (127) is the maximum in MIDI; 40 is the middle of the sample, 20 1/4th etc.
  0A - Arpeggiator
    Arpeggiate. X and Y are note offsets in semitones. 0 continues previous value.
  0B - Retrigger
    Retrigger note.
    X - Volume reduction per trigger
    Y - Retrigger count
  0C - Sample probability
  60 - Reverse
    Reverses playhead if effect value is 00. Note that if this effect is applied on a 
    note start, it plays the sample from the end. If the effect is started from anywhere 
    else, it will simply reverse the playhead. When the effect is used with 01, hackey 
    tracker playback will return to forward playing.
";
bx = by = 10;
gfx_measurestr(str, ww, hh);
gfx_set(0, 0, 0, 0.9);
gfx_rect(bx, by, ww + 2 * bx, hh + 2 * by);
gfx_set(1, 1, 1, 1);
gfx_x = 2 * bx;
gfx_y = 2 * by;
gfx_printf(str);
);

last_char = current_char;

/*
gfx_x = gfx_y = 0;
gfx_set(1, 1, 1, 1);
gfx_printf(DROPPED_FILE_STR);
*/

hinter.drawhint();
old_time = new_time;
new_time = time_precise();
delta_time = new_time - old_time;

